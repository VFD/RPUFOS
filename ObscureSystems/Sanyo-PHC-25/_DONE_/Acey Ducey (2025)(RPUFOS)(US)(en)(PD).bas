10 REM -------------------------
20 REM Acey Ducey Card Game
30 REM 
40 REM Retro Programers United
50 REM For Obscure System
60 REM 
70 REM 
80 REM VincentD
90 REM -------------------------
1000 REM ------------------
1010 REM BEGIN
1020 REM ------------------
1030 GOSUB 9030:     REM Title
1040 GOSUB 9730:     REM Rules?
1050 CLS:GOSUB 9130: REM SCR
1060 GOSUB 9530:     REM Init
1070 REM A+INT((B-A+1)*RND(1))
1080 LOCATE 2,4:PRINT Q;"    "
1090 GOSUB 9630: REM Erase cards
1100 I=RND(-RND(1)*100)
1110 A=2+INT((14-2+1)*RND(1)): REM [2,14]
1120 I=RND(-RND(1)*100)
1130 B=A+INT((14-A+1)*RND(1)): REM [A,14]
1140 REM Card One
1150 IF A<11 THEN LOCATE X1,Y1:PRINT" ";A;"  ":GOTO 1210
1160 IF A=11 THEN LOCATE X1,Y1:PRINT"Jack":GOTO 1210
1170 IF A=12 THEN LOCATE X1,Y1:PRINT"Queen":GOTO 1210
1180 IF A=13 THEN LOCATE X1,Y1:PRINT"King":GOTO 1210
1190 IF A=14 THEN LOCATE X1,Y1:PRINT" Ace"
1200 REM Card Two
1210 IF B<11 THEN LOCATE X2,Y2:PRINT" ";B;"  ":GOTO 1260
1220 IF B=11 THEN LOCATE X2,Y2:PRINT"Jack":GOTO 1260
1230 IF B=12 THEN LOCATE X2,Y2:PRINT"Queen":GOTO 1260
1240 IF B=13 THEN LOCATE X2,Y2:PRINT"King":GOTO 1260
1250 IF B=14 THEN LOCATE X2,Y2:PRINT" Ace"
1260 GOSUB 9250
1270 INPUT"Your bet:";M
1280 IF M<>0 THEN 1310
1290 PRINT"Chicken!!!"
1300 GOTO 1080
1310 IF M<=Q THEN 1370
1320 PRINT"Sorry, my friend,":PRINT"but you bet too much."
1330 PRINT"You have only ";Q;"$ to bet."
1340 GOTO 1270
1350 REM Card Three
1360 I=RND(-RND(1)*100)
1370 C=2+INT((14-2+1)*RND(1))
1380 IF C<11 THEN LOCATE X3,Y3:PRINT" ";C;"  ":GOTO 1430
1390 IF C=11 THEN LOCATE X3,Y3:PRINT"Jack":GOTO 1430
1400 IF C=12 THEN LOCATE X3,Y3:PRINT"Queen":GOTO 1430
1410 IF C=13 THEN LOCATE X3,Y3:PRINT"King":GOTO 1430
1420 IF C=14 THEN LOCATE X3,Y3:PRINT" Ace"
1430 GOSUB 9250
1440 IF C>A THEN 1460
1450 GOTO 1490
1460 IF C>=B THEN 1490
1470 PRINT"You win.": GOSUB 9930
1480 Q=Q+M: GOTO 1080
1490 PRINT"Sorry, you lose.": GOSUB 9930
1500 IF M<Q THEN Q=Q-M:GOTO 1080
1510 REM
1520 PRINT"Sorry, friend, but you blew your wad."
1530 GOSUB 9840
1540 REM -----------
1550 REM END of game
1560 REM -----------
8000 REM -----------------
8010 REM Game rules
8020 REM -----------------
8030 CLS
8040 PRINT"Acey-ducey is played in the"
8050 PRINT"following manner:"
8060 PRINT"The dealer (computer) deals two"
8070 PRINT"cards face up."
8080 PRINT"You have an option to bet or not";
8090 PRINT"bet depending on whether or not"
8100 PRINT"you feel the third card will"
8110 PRINT"have a value between the first"
8120 PRINT"two."
8130 PRINT"If you do not want to bet, input";
8140 PRINT"a 0."
8150 PRINT
8160 PRINT"Press a key to continu.";
8180 GOSUB 9930
8190 RETURN
9000 REM ------------------
9010 REM Game Title
9020 REM ------------------
9030 SCREEN 1,1,1:CLS
9040 COLOR 2,,1:CONSOLE 0,3:CLS
9050 PRINT TAB(6);"ACEY DUCEY card game"
9060 PRINT TAB(7);"Creative Computing"
9070 PRINT TAB(5);"Morristown, New Jersey";
9080 COLOR 1,,1:CONSOLE 3,13:CLS
9090 RETURN
9100 REM -----------------------
9110 REM Game SCR
9120 REM -----------------------
9130 RESTORE 9180
9140 FOR I=&h60C0 TO &h615F
9150   READ A$: A$="&H"+A$
9160   POKE I,VAL(A$)
9170 NEXT I
9180 DATA 20,18,17,17,17,31,17,17,17,19,20,18,17,17,17,32,17,17,17,19,20,18,17,17,17,33,17,17,17,19,20,20
9190 DATA 20,16,20,20,20,20,20,20,20,16,20,16,20,20,20,20,20,20,20,16,20,16,20,20,20,20,20,20,20,16,20,20
9200 DATA 20,16,20,20,20,20,20,20,20,16,20,16,20,20,20,20,20,20,20,16,20,16,20,20,20,20,20,20,20,16,20,20
9210 DATA 20,16,20,20,20,20,20,20,20,16,20,16,20,20,20,20,20,20,20,16,20,16,20,20,20,20,20,20,20,16,20,20
9220 DATA 20,1A,17,17,17,17,17,17,17,1B,20,1A,17,17,17,17,17,17,17,1B,20,1A,17,17,17,17,17,17,17,1B,20,20
9230 LOCATE 0,4:PRINT"$"
9240 REM Define console for interactions
9250 CONSOLE 12,4:CLS
9260 RETURN
9270 REM
9280 REM
9290 REM
9500 REM --------
9510 REM Init var
9520 REM --------
9530 Q=100:REM Money
9540 X1=3:Y1=8:REM card 1 name pos
9550 X2=13:Y2=8:REM card 2 name pos
9560 X3=23:Y3=8:REM card 3 name pos
9570 REM
9580 I=RND(-RND(1)*100):REM =RANDOMIZE
9590 RETURN
9600 REM ----------------
9610 REM Erase Cards Name
9620 REM ----------------
9630 LOCATE X1,Y1:PRINT SPC(5)
9640 LOCATE X2,Y2:PRINT SPC(5)
9650 LOCATE X3,Y3:PRINT SPC(5)
9660 RETURN
9670 REM
9680 REM
9690 REM
9700 REM -------------
9710 REM Instructions?
9720 REM -------------
9730 LOCATE 4,4:PRINT"Do you want instructions"
9740 LOCATE 13,5:PRINT"(Y/y)"
9750 GOSUB 9930
9760 IF LEFT$(K$,1)="Y" OR LEFT$(K$,1)="y" THEN GOSUB 8030
9770 RETURN
9780 REM 
9790 REM 
9800 REM ---------------------
9810 REM Play again subroutine
9830 REM ---------------------
9840 PRINT"Another GAME (Y/y)?"
9850 GOSUB 9930
9860 IF LEFT$(K$,1)="Y" OR LEFT$(K$,1)="y" THEN RUN: REM Garbage collector
9870 CONSOLE 0,16:COLOR 1,,1:CLS
9880 PRINT"O.K., hope you had fun!"
9890 GOTO 9999
9900 REM -------------------
9910 REM Key wait subroutine
9920 REM -------------------
9930 K$="":K$=INKEY$:IF k$="" THEN 9930
9940 RETURN
9999 END